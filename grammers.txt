1. Program -> Declaration-list $
2. Declaration-list -> Declaration Declaration-list | EPSILON
3. Declaration -> Declaration-initial Declaration-prime
4. Declaration-initial -> Type-specifier ID
5. Declaration-prime -> Fun-declaration-prime | Var-declaration-prime
6. Var-declaration-prime -> ; | [ NUM ] ;
7. Fun-declaration-prime -> ( Params ) Compound-stmt
8. Type-specifier -> int | void
9. Params -> int ID Param-prime Param-list | void
10. Param-list -> , Param Param-list | EPSILON
11. Param -> Declaration-initial Param-prime
12. Param-prime -> [ ] | EPSILON
13. Compound-stmt -> { Declaration-list Statement-list }
14. Statement-list -> Statement Statement-list | EPSILON
15. Statement -> Expression-stmt | Compound-stmt | Selection-stmt | Iteration-stmt | Return-stmt
16. Expression-stmt -> Expression ; | break ; | ;
17. Selection-stmt -> if ( Expression ) Statement Else-stmt
18. Else-stmt -> endif | else Statement endif
19. Iteration-stmt -> repeat Statement until ( Expression )
20. Return-stmt -> return Return-stmt-prime
21. Return-stmt-prime -> ; | Expression ;
22. Expression -> Simple-expression-zegond | ID B
23. B -> = Expression | [ Expression ] H | Simple-expression-prime
24. H -> = Expression | G D C
25. Simple-expression-zegond -> Additive-expression-zegond C
26. Simple-expression-prime -> Additive-expression-prime C
27. C -> Relop Additive-expression | EPSILON
28. Relop -> < | ==
29. Additive-expression -> Term D
30. Additive-expression-prime -> Term-prime D
31. Additive-expression-zegond -> Term-zegond D
32. D -> Addop Term D | EPSILON
33. Addop -> + | -
34. Term -> Factor G
35. Term-prime -> Factor-prime G
36. Term-zegond -> Factor-zegond G
37. G -> * Factor G | EPSILON
38. Factor -> ( Expression ) | ID Var-call-prime | NUM
39. Var-call-prime -> ( Args ) | Var-prime
40. Var-prime -> [ Expression ] | EPSILON
41. Factor-prime -> ( Args ) | EPSILON
42. Factor-zegond -> ( Expression ) | NUM
43. Args -> Arg-list | EPSILON
44. Arg-list -> Expression Arg-list-prime
45. Arg-list-prime -> , Expression Arg-list-prime | EPSILON